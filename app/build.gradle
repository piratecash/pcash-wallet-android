// NB: Android Studio can't find the imports; this does not affect the
// actual build since Gradle can find them just fine.
// remove after upgrading to 'com.android.tools.build:gradle:8.1.0' (AGP 8.1)
import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose' version('2.2.0')
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'androidx.navigation.safeargs.kotlin'
}

// Git version information helpers (using modern providers.exec API)
def gitHashProvider = providers.exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    ignoreExitValue = true
}.standardOutput.asText.map { it.trim() }

def gitBranchProvider = providers.exec {
    commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
    ignoreExitValue = true
}.standardOutput.asText.map {
    def branch = (it ?: "").trim()
    (branch.isEmpty() || branch == "HEAD") ? "unknown" : branch
}

def gitDirtyProvider = providers.exec {
    commandLine 'git', 'diff-index', '--quiet', 'HEAD', '--'
    ignoreExitValue = true
}.result.map { it.exitValue != 0 }

def getGitVersionSuffix = { ->
    try {
        def hash = gitHashProvider.getOrElse("unknown")
        if (hash == "unknown" || hash.isEmpty()) {
            println "GIT_HASH: unknown (empty or failed)"
            return "unknown"
        }
        def dirty = gitDirtyProvider.getOrElse(false) ? "-dirty" : ""
        def fdroid = (gitBranchProvider.getOrElse("") == "f-droid") ? "-fdroid" : ""
        def result = "${hash}${dirty}${fdroid}"
        println "GIT_HASH: ${result}"
        return result
    } catch (Exception ignored) {
        println "GIT_HASH: unknown (exception: ${ignored.message})"
        return "unknown"
    }
}

android {

    defaultConfig {
        applicationId "cash.p.terminal"
        compileSdk compile_sdk_version
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode 222
        versionName "0.51.18"
        buildConfigField "String", "GIT_HASH", "\"${getGitVersionSuffix()}\""
        buildConfigField "String", "BITCOIN_KIT_VERSION", "\"${libs.versions.piratecash.bitcoin.get()}\""
        buildConfigField "String", "ETHEREUM_KIT_VERSION", "\"${libs.versions.piratecash.ethereum.get()}\""
        buildConfigField "String", "SOLANA_KIT_VERSION", "\"${libs.versions.piratecash.solana.get()}\""
        buildConfigField "String", "TON_KIT_VERSION", "\"${libs.versions.piratecash.ton.get()}\""
        buildConfigField "String", "TRON_KIT_VERSION", "\"${libs.versions.piratecash.tron.get()}\""
        buildConfigField "String", "MONERO_KIT_VERSION", "\"${libs.versions.monero.kit.android.get()}\""
        buildConfigField "String", "STELLAR_KIT_VERSION", "\"${libs.versions.stellar.kit.get()}\""
        buildConfigField "String", "ZCASH_SDK_VERSION", "\"${libs.versions.zcashAndroidSdk.get()}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranchProvider.getOrElse("unknown")}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = []
        lintOptions {
            checkReleaseBuilds false
        }

        resValue "string", "companyWebPageLink", "https://p.cash/"
        resValue "string", "appWebPageLink", "https://p.cash/#MOBILE_APPS"
        resValue "string", "analyticsLink", "https://p.cash/"
        resValue "string", "appGithubLink", "https://github.com/piratecash/pcash-wallet-android"
        resValue "string", "appTwitterLink", "https://twitter.com/PirateCash_NET"
        resValue "string", "appTelegramLink", "https://t.me/pcash"
        resValue "string", "appRedditLink", "https://www.reddit.com/r/PirateCash/"
        resValue "string", "reportEmail", "i@p.cash"
        resValue "string", "walletConnectAppMetaDataName", "pcash"
        resValue "string", "walletConnectAppMetaDataUrl", "p.cash"
        resValue "string", "walletConnectAppMetaDataIcon", "https://raw.githubusercontent.com/piratecash/pcash-wallet-ios/master/UnstoppableWallet/UnstoppableWallet/AppIcon.xcassets/AppIcon.appiconset/p.cash_1024.png"
        resValue "string", "accountsBackupFileSalt", "pcash"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }

    aaptOptions {
        cruncherEnabled = false
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    signingConfigs {
        appCenter {
            storeFile file("./test.keystore")
            storePassword "testKeystore123"

            keyAlias "testKeystore"
            keyPassword "testKeystore123"
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".dev"
            resValue "string", "uniswapGraphUrl", "https://api.thegraph.com/subgraphs/name/uniswap/uniswap-v2"
            resValue "string", "is_release", "false"
            resValue "string", "guidesUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/refs/tags/v1.4/src/edu.json"
            resValue "string", "faqUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/master/src/faq.json"
            resValue "string", "coinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/master/coins.json"
            resValue "string", "providerCoinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/master/provider.coins.json"
//            resValue "string", "marketApiBaseUrl", "https://api-dev.blocksdecoded.com"
//            resValue "string", "marketApiKey", "IQf1uAjkthZp1i2pYzkXFDom"
            resValue "string", "blocksDecodedEthereumRpc", "https://api-dev.blocksdecoded.com/v1/ethereum-rpc/mainnet"
            resValue "string", "chainalysisBaseUrl", "https://public.chainalysis.com/api/v1/"
            resValue "string", "hashDitBaseUrl", "https://service.hashdit.io/v2/hashdit/"
            resValue "string", "alphaAmlBaseUrl", "https://api.alpha-aml.com"
        }

        appcenterdebug {
            initWith debug
            defaultConfig.versionCode System.getenv("BUILD_NUMBER")?.toInteger() ?: defaultConfig.versionCode
            applicationIdSuffix ".dev.appcenter"
            signingConfig signingConfigs.appCenter
            matchingFallbacks = ['debug']
        }

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "uniswapGraphUrl", "https://api.thegraph.com/subgraphs/name/uniswap/uniswap-v2"
            resValue "string", "is_release", "true"
            resValue "string", "guidesUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/refs/tags/v1.4/src/edu.json"
            resValue "string", "faqUrl", "https://raw.githubusercontent.com/horizontalsystems/Unstoppable-Wallet-Website/v1.3/src/faq.json"
            resValue "string", "coinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/v0.21/coins.json"
            resValue "string", "providerCoinsJsonUrl", "https://raw.githubusercontent.com/horizontalsystems/cryptocurrencies/v0.21/provider.coins.json"
//            resValue "string", "marketApiBaseUrl", "https://api.blocksdecoded.com"
//            resValue "string", "marketApiKey", "IQf1uAjkthZp1i2pYzkXFDom"
            resValue "string", "blocksDecodedEthereumRpc", "https://api.blocksdecoded.com/v1/ethereum-rpc/mainnet"
            resValue "string", "chainalysisBaseUrl", "https://public.chainalysis.com/api/v1/"
            resValue "string", "hashDitBaseUrl", "https://service.hashdit.io/v2/hashdit/"
            resValue "string", "alphaAmlBaseUrl", "https://api.alpha-aml.com"
        }

        appcenterrelease {
            initWith release
            defaultConfig.versionCode System.getenv("BUILD_NUMBER")?.toInteger() ?: defaultConfig.versionCode
            applicationIdSuffix ".appcenter"
            signingConfig signingConfigs.appCenter
            matchingFallbacks = ['release']
        }

    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        resources {
            pickFirsts += ['META-INF/atomicfu.kotlin_module']
            excludes += ['META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE.md', 'META-INF/NOTICE.md']
        }
        jniLibs {
            useLegacyPackaging = true
        }
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    namespace 'cash.p.terminal'
    lint {
        disable 'LogNotTimber'
    }

    def bcprovVersionsToRedirect = [
            'bcprov-jdk15on:1.65.01',
            'bcprov-jdk15on:1.66',
            'bcprov-jdk15on:1.70',
            'bcprov-jdk15to18:1.65.01',
            'bcprov-jdk15to18:1.68',
            'bcprov-jdk15to18:1.69'
    ]

    configurations.all { c ->
        exclude(group: "com.google.firebase")
        exclude(group: "com.google.android.gms")

        exclude(group: "com.github.metaplex-foundation.kborsh", module: "kborsh-jvm")

        exclude(group: "com.walletconnect.Scarlet", module: "scarlet-core")
        exclude(group: "com.walletconnect.Scarlet", module: "scarlet")
        exclude(group: "com.walletconnect.Scarlet", module: "websocket-okhttp")
        exclude(group: "com.walletconnect.Scarlet", module: "stream-adapter-rxjava2")
        exclude(group: "com.walletconnect.Scarlet", module: "message-adapter-gson")
        exclude(group: "com.walletconnect.Scarlet", module: "lifecycle-android")

        c.resolutionStrategy.dependencySubstitution {
            bcprovVersionsToRedirect.each { moduleId ->
                def (name, version) = moduleId.split(':')
                substitute module("org.bouncycastle:$name:$version") using module("org.bouncycastle:bcprov-jdk15to18:1.80")
            }

            substitute module('com.google.protobuf:protobuf-java:3.6.1') using module('com.google.protobuf:protobuf-javalite:3.21.1')
            substitute module('net.jcip:jcip-annotations:1.0') using module('com.github.stephenc.jcip:jcip-annotations:1.0-1')

            substitute module('com.tinder.scarlet:scarlet:0.1.12') using module('com.github.WalletConnect.Scarlet:scarlet:1.0.2')
            substitute module('com.tinder.scarlet:websocket-okhttp:0.1.12') using module('com.github.WalletConnect.Scarlet:websocket-okhttp:1.0.2')
            substitute module('com.tinder.scarlet:stream-adapter-rxjava2:0.1.12') using module('com.github.WalletConnect.Scarlet:stream-adapter-rxjava2:1.0.2')
            substitute module('com.tinder.scarlet:message-adapter-gson:0.1.12') using module('com.github.WalletConnect.Scarlet:message-adapter-gson:1.0.2')
            substitute module('com.tinder.scarlet:lifecycle-android:0.1.12') using module('com.github.WalletConnect.Scarlet:lifecycle-android:1.0.2')
            substitute(module("com.reown:android-core")).using(module("com.github.piratecash.reown-kotlin:android:d413f9b"))
        }

        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.okhttp3') {
                details.useVersion "4.12.0"
            }
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def versionName = variant.versionName
                def newApkName = "p.cash-${versionName}.apk"
                output.outputFileName = newApkName
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':core:ui-compose')
    implementation libs.appcompat
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation(libs.kotlinx.serialization.json)

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // use -ktx for Kotlin
    // alternatively - just LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // alternatively - Lifecycles only (no ViewModel or LiveData).
    //     Support library depends on this lightweight import
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation libs.androidx.fragment.ktx
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation libs.bitcoinj

    //Splash screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    //AppWidgets
    implementation 'androidx.glance:glance-appwidget:1.1.1'

    // Room
    implementation libs.room.runtime
    implementation libs.room.ktx
    implementation libs.androidx.room.rxjava2
    implementation libs.room.paging
    implementation libs.androidx.material3.android
    ksp libs.room.compiler

    // Paging
    implementation libs.paging.runtime
    implementation libs.paging.compose

    testImplementation libs.junit.jupiter
    // alternately - if using Java8, use the following instead of compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    implementation libs.androidx.recyclerview

    implementation(platform(libs.koin.bom))
    implementation(libs.koin.compose.viewmodel)

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "androidx.biometric:biometric:$biometric_version"

    implementation libs.richtext.markdown
    implementation libs.richtext.ui.material3
    implementation libs.richtext.commonmark

    // ViewPager circle indicator
    implementation 'me.relex:circleindicator:2.1.6'

    //Custom tabs, chrome
    implementation "androidx.browser:browser:1.8.0"

    // Navigation component
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ktx_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ktx_version"

    //Compose Navigation
    implementation libs.androidx.navigation.compose
    implementation libs.accompanist.navigation.animation

    // QR code scanning with CameraX + ZXing core
    implementation libs.camerax.camera2
    implementation libs.camerax.lifecycle
    implementation libs.camerax.view
    implementation libs.zxing.core

    // QR code generator
    implementation("io.github.alexzhirkevich:qrose:1.0.1")

    // WorkManager Kotlin
    def work_version = "2.10.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    // WorkManager RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"

    def leakCanary = libs.leakcanary.android
    appcenterdebugImplementation leakCanary
    debugImplementation leakCanary

    // Wallet kits
    implementation(libs.ton.kit)
    implementation(libs.ton.kotlin.contract)
    implementation libs.bitcoin.kit
    implementation libs.ethereum.kit
    implementation libs.solana.kit
    implementation libs.stellar.kit

    implementation libs.monero.kit
    implementation libs.netcipher
    implementation libs.solanakt
    implementation 'com.github.horizontalsystems:blockchain-fee-rate-kit-android:1d3bd49'
    implementation libs.tron.kit
    // Zcash SDK
    coreLibraryDesugaring libs.desugar.jdk.libs
    implementation libs.zcash.android.sdk
    implementation(libs.grpc.api)

    // Exclude old version from wherever it's coming
    configurations.configureEach {
        exclude group: "org.bouncycastle", module: "bcprov-jdk18on"
        exclude group: "org.bouncycastle", module: "bcutil-jdk18on"
    }

    // WalletConnect V2
    implementation(platform(libs.android.bom))
    implementation libs.reown.walletkit
    implementation libs.reown.core

    // Unstoppable Domains
    implementation 'com.github.unstoppabledomains:resolution-java:v7.1.0'

    // Ethereum Name Service
    implementation libs.web3j.core

    implementation 'com.twitter.twittertext:twitter-text:3.1.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.8.1'

    // UI modules

    implementation project(':core:core')
    implementation project(':core:strings')
    implementation project(':core:wallet')
    implementation project(':core:resources')
    implementation project(':core:network')
    implementation project(':core:navigation')
    implementation project(':components:icons')
    implementation project(':components:chartview')
    implementation project(':feature-stacking')
    implementation project(':feature-tangem')
    implementation project(':feature-premium')
    implementation project(':feature-logging')
    implementation project(':feature-mini-app')

    // Integration with activities
    implementation libs.androidx.activity.compose
    // Compose Material Design
    implementation "androidx.compose.material:material:$compose_version"
    // Animations
    implementation "androidx.compose.animation:animation:$compose_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    // Lifecycle utilities for Compose
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    implementation libs.coil.compose
    implementation libs.coil.network.okhttp
    implementation libs.coil.svg
    implementation libs.coil.gif
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'

    // When using a AppCompat theme
    implementation libs.accompanist.appcompat.theme
    implementation libs.accompanist.flowlayout
    implementation libs.accompanist.permissions

    // UI Tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // optional - Test helpers for LiveData
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.powermock.api.mockito2
    testImplementation libs.powermock.module.junit4
    testImplementation libs.koin.test.junit4
    implementation libs.ktor.client.core
    implementation libs.ktor.client.okhttp

    // Spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:2.0.9"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:2.0.9"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //Android Tor
    implementation libs.tor.android

    api 'com.jaredrummler:android-shell:1.0.0'
    api 'com.offbynull.portmapper:portmapper:2.0.5'

    implementation libs.tangem.sdk
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

task copyChangelogs(type: Copy) {
    description = 'Copy changelog files from root to assets'
    group = 'build'

    from rootProject.projectDir
    into 'src/main/assets/common'
    include 'CHANGELOG_*.md'
    include 'changelog_*.md'

    outputs.upToDateWhen { false }

    doFirst {
        file('src/main/assets/common').mkdirs()
        println "Copying changelog files..."
    }

    doLast {
        println "Changelog files copied successfully"
    }
}

preBuild.dependsOn copyChangelogs
